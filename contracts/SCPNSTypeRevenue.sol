// SPDX-License-Identifier: MIT

pragma solidity ^0.8.2;

import "./SCPNSBase.sol";
import "./interfaces/ISCPNSTypeUnit.sol";
import "./interfaces/ISCPNSTypeRevenue.sol";
import "./ContractProject.sol";
import "./PairValues.sol";

/**
 * @dev {SCPNSTypeRevenue} token, including:
 *
 *  - ability for holders to burn (destroy) their tokens
 *  - a minter role that allows for token minting (creation)
 *  - a pauser role that allows to stop all token transfers
 *  - token ID and URI autogeneration
 *
 * This contract uses {AccessControl} to lock permissioned functions using the
 * different roles - head to its documentation for details.
 *
 * The account that deploys the contract will be granted the minter and pauser
 * roles, as well as the default admin role, which will let it grant both minter
 * and pauser roles to other accounts.
 */
contract SCPNSTypeRevenue is 
    SCPNSBase,
    ContractProject,
    ISCPNSTypeRevenue
    {
    using CountersUpgradeable for CountersUpgradeable.Counter;
    using PairValues for PairValues.PairUint256;

    // Mapping from id to value
    mapping (uint256 => uint256) private _id2Value;
    // Mapping from id to min value of typeUnitId
    mapping (uint256 => mapping(uint256 => uint256)) _id2MinOfTypeUnit;
    // Mapping from id to max value of typeUnitId
    mapping (uint256 => mapping(uint256 => uint256)) _id2MaxOfTypeUnit;
    // Mapping from typeUnitId to id
    PairValues.PairUint256 private _typeUnitId2defaultIds;

    function initialize(address dns) public virtual initializer {
        __SCPNSBase_init("SCPNSTypeRevenue", "SCPNSTypeRevenue", "");
        __ContractProject_init(dns);
        __SCPNSTypeRevenue_init();
    }
    /**
     * @dev Grants `DEFAULT_ADMIN_ROLE`, `MINTER_ROLE` and `PAUSER_ROLE` to the
     * account that deploys the contract.
     *
     * Token URIs will be autogenerated based on `baseURI` and their token IDs.
     */
    function __SCPNSTypeRevenue_init() internal initializer {
        __SCPNSTypeRevenue_init_unchained();
    }

    function __SCPNSTypeRevenue_init_unchained() internal initializer {
        _unitType("typerevenue");
    }

    /**
     * @dev Creates a new token for `to`. Its token ID will be tokenId
     * URI autogenerated based on the base URI passed at construction.
     *
     * Requirements:
     *
     * - the caller must have the `MINTER_ROLE`.
     */
    function mint(uint256 tokenId, bytes32 name_, uint256 value, string memory datas) public virtual override whenNotPaused {
        _mint(_msgSender(), tokenId, name_, datas);

        _id2Value[tokenId] = value;
    }

    function setDefaultTokenOf(uint256 typeUnitId, uint256 tokenId) public virtual override whenNotPaused {
        require(hasRole(MANAGER_ROLE, _msgSender()) || _msgSender() == super.ownerOf(tokenId), 
                "SCPNSBase: must have manager role");
        require(_exists(tokenId), "SCPNSTypeRevenue: token is nonexists");
        require(_typeUnitIf().exists(typeUnitId), "SCPNSTypeRevenue: typeUnitId is nonexists");

        _typeUnitId2defaultIds.set(typeUnitId, tokenId);
    }

    function typeUnitIds() public view virtual override returns(uint256[] memory) {
        return _typeUnitId2defaultIds.keysOf();
    }

    function setComputilityRange(uint256 tokenId, uint256 typeUnitId, uint256 min, uint256 max) public virtual override whenNotPaused {
        require(hasRole(MANAGER_ROLE, _msgSender()) || _msgSender() == super.ownerOf(tokenId), 
                "SCPNSBase: must have manager role");
        require(_exists(tokenId), 
                "SCPNSTypeRevenue: token is nonexists.");
        require(_typeUnitIf().exists(typeUnitId), 
                "SCPNSTypeRevenue: typeUnitId is nonexists");
        require(min <= max, 
                "SCPNSTypeRevenue:  min must be less than or equal to max");

        _id2MinOfTypeUnit[tokenId][typeUnitId] = min;
        _id2MaxOfTypeUnit[tokenId][typeUnitId] = max;

    }

    function rvalueOf(uint256 tokenId) public view virtual override returns(uint256) {
        require(_exists(tokenId), 
                "SCPNSTypeRevenue: tokenId is nonexists");

        return _id2Value[tokenId];
    }

    function valueOfTypeUnitId(uint256 typeUnitId) public view virtual override returns(uint256) {
        require(_typeUnitId2defaultIds.exists(typeUnitId), 
                "SCPNSTypeRevenue: typeUnitId is nonexists");

        return _id2Value[_typeUnitId2defaultIds.valueOf(typeUnitId)];
    }
    function tokenIdOfTypeUnitId(uint256 typeUnitId) public view virtual override returns(uint256) {
        require(_typeUnitId2defaultIds.exists(typeUnitId), 
                "SCPNSTypeRevenue: typeUnitId is nonexists");

        return _typeUnitId2defaultIds.valueOf(typeUnitId);
    }

    function computilityRangeOfTypeUnit(uint256 tokenId, uint256 typeUnitId) public view virtual override returns(uint256 min, uint256 max) {
        require(_exists(tokenId), 
                "SCPNSTypeRevenue: token is nonexists.");
        require(_typeUnitIf().exists(typeUnitId), 
                "SCPNSTypeRevenue: typeUnitId is nonexists");

        min = _id2MinOfTypeUnit[tokenId][typeUnitId];
        max = _id2MaxOfTypeUnit[tokenId][typeUnitId];
    }

    function _burn(uint256 tokenId) internal virtual override(SCPNSBase) {
        require(false, 
                "SCPNSTypeRevenue: can't burn anyone token");
        require(_exists(tokenId), 
                "SCPNSTypeRevenue: token is noexists");
    }

    uint256[48] private __gap;
}
